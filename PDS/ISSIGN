***********************************************************************
* ISSIGN - PRODUCTION CICS SIGNON PROGRAM                             *
* ======                                                              *
*                                                                     *
*    This program is a user friendly CICS/RACF signon program for     *
*    production CICS.                                                 *
*                                                                     *
*    It presents the user with a signon panel giving two choices :    *
*                                                                     *
*      1 - The user may enter his user id and password (and also      *
*          a new password if required). If the userid and password    *
*          are valid, he is logged onto CICS.                         *
*                                                                     *
*      2 - The user may just press PF3 or CLEAR to logoff.            *
*                                                                     *
***********************************************************************
* Author : N.G.Thomas.                                                *
***********************************************************************
*                 M A I N T E N A N C E    L O G                      *
*---------------------------------------------------------------------*
*                                                                     *
*    DATE    WHO  DESCRIPTION OF CHANGE                               *
*  ==/==/==  ===  ==================================================  *
*                                                                     *
*  14/10/88  NGT  If the SIT message starts with 'xxxx:', then that   *
*                 portion of the message is not displayed.            *
*                                                                     *
*  31/05/89  NGT  Leading blanks will be removed from input fields.   *
*                 (this was causing a RACF internal error message).   *
*                                                                     *
*  19/02/91  NGT  New version created for the new employer            *
*                                                                     *
*  08/05/91  NGT  Modified to be able to run amode/rmode 31.          *
*                                                                     *
*  19/10/97  NGT  Modified to run under CICS 4.1 using CICS security  *
*                 API rather than the undocumented RACF interface     *
*                                                                     *
*  25/03/98  NGT  If a transaction name is prefixed to the good       *
*                 morning message (eg xxx:message text), it is used   *
*                 as the initial transaction. This allows a CICS      *
*                 application menu to be displayed.                   *
*                                                                     *
***********************************************************************
         SPACE
DFHEISTG DSECT
         COPY ISSIGNMD
         SPACE
COMMAR   DS   CL8                     Comms area
         SPACE
ABSTIME  DS   D                       Absolute Time
         SPACE
TRANSID  DS   CL4                     Transaction to run first
         SPACE
SONID    DS   CL8                     Signed on ID
DEFID    DS   CL8                     Default ID
         SPACE
USERID   DS   CL8                     Userid to logon to RACF with
         SPACE
USER     DS   CL8                     Userid
PASSWD   DS   CL8                     Password
NPASSWD  DS   CL8                     New Password
         SPACE
RESP     DS   F
RESP2    DS   F
         SPACE
GMMLEN   DS   H                       Good Morning Message length
GMMTEXT  DS   CL246                   Good Morning Message
         SPACE
ERRMSG   DS   CL70                    Area to build error message
         EJECT
ISSIGN   CSECT
         DFHREGS
TCTTEAR  EQU    R5
         COPY   DFHAID
         SPACE
***********************************************************************
*  INITIAL ENTRY CHECK                                                *
*  ===================                                                *
*                                                                     *
*    If we have been entered with a transaction ID of LOGO then       *
*    logoff.                                                          *
*                                                                     *
*    Check to see if a commarea exists. If it does, then we have      *
*    been entered for a second time, and should process any entered   *
*    data.                                                            *
*                                                                     *
*    If there is no commarea, then we shall build the signon screen   *
*    and send it to the terminal.                                     *
*                                                                     *
***********************************************************************
         SPACE
         CLC   EIBTRNID,LOGO           Entered for LOGOFF ?
         BE    LOGOFF                  Yes, then do so
         SPACE
         OC    EIBCALEN,EIBCALEN       IS THERE A COMMAREA ?
         BNZ   DORECV                  YES, GO RECEIVE ANY INPUT
         SPACE
***********************************************************************
* First time in processing                                            *
***********************************************************************
         SPACE
         BAL   R9,SIGNOFF              SIGN OFF RACF (IF REQUIRED)
         SPACE
         MVC   SPUSERL,PUTCURS         PUT CURSOR IN USER FIELD
         SPACE
***********************************************************************
* Build the signon map                                                *
***********************************************************************
         SPACE
         EXEC  CICS ASSIGN                                             X
               APPLID(SPSYSO)
         SPACE
         EXEC  CICS ASKTIME                                            X
               ABSTIME(ABSTIME)
         SPACE
         EXEC  CICS FORMATTIME                                         X
               ABSTIME(ABSTIME)                                        X
               DATESEP('/')                                            X
               DDMMYY(SPDATEO)                                         X
               TIMESEP                                                 X
               TIME(SPTIMEO)
         SPACE
         MVC   SPTERMO,EIBTRMID        MOVE TERMINAL NAME TO MAP
         SPACE
***********************************************************************
* Move the good morning message to the map (from the SIT)             *
*                                                                     *
*  Note : If the message starts 'xxxx:', then the assumption is made  *
*         that 'xxxx' is the first transaction to be run.             *
*                                                                     *
***********************************************************************
         SPACE
         EXEC CICS INQUIRE SYSTEM                                      X
               GMMLENGTH(GMMLEN)                                       x
               GMMTEXT(GMMTEXT)
         LH    R5,GMMLEN               GET LENGTH OF MESSAGE
         LTR   R5,R5                   IS THERE REALLY ONE ?
         BZ    NOGMM                   NO
         SPACE
         LA    R4,GMMTEXT              -> Text of message
         CH    R5,H5                   IS IT LONG ENOUGH FOR A PREFIX ?
         BNH   NOSITPRE                NO, SKIP THE CHECK THEN
         SPACE
         CLI   4(R4),C':'              DOES IT HAVE A PREFIX ?
         BNE   NOSITPRE                NO, GO CENTRE IT
         SH    R5,H5                   REDUCE LENGTH BY 5
         MVC   SPNXTO(4),0(R4)         move first transaction to map
         LA    R4,5(,R4)               SKIP THE PREFIX
         SPACE
NOSITPRE DS    0H
         CH    R5,MAXGMM               TOO LONG ?
         BNH   CENGMM                  NO, GO CENTER IT
         LH    R5,MAXGMM               YES, TRUNCATE
         LA    R6,SPGMMTO              -> PLACE TO MOVE TO
         B     MVGMM                   GO MOVE IT
         SPACE
CENGMM   DS    0H
         LH    R7,MAXGMM               GET MAX LENGTH
         SR    R7,R5                   CALC NO. OF SLACK BYTES
         SRL   R7,1                    DIVIDE BY TWO
         MVI   SPGMMTO,C' '
         MVC   SPGMMTO+1(L'SPGMMTO-1),SPGMMTO
         LA    R6,SPGMMTO              -> OUTPUT AREA
         LA    R6,0(R7,R6)             CENTRE IT
MVGMM    DS    0H
         BCTR  R5,0                    -1 FOR EXECUTE
         EX    R5,MVGMMT               MOVE TO MAP
MVGMMT   MVC   0(0,R6),0(R4)
         SPACE
NOGMM    DS    0H
         SPACE
***********************************************************************
* WRITE THE SIGNON SCREEN TO THE TERMINAL                             *
***********************************************************************
         SPACE
         EXEC  CICS SEND                                               X
               MAP('ISSIGN')                                           X
               MAPSET('ISSIGNM')                                       X
               ERASE                                                   X
               CURSOR
         SPACE
RETURN   DS    0H
         SPACE
***********************************************************************
* RETURN TO CICS AND WAIT FOR THE USERS ACTION                        *
***********************************************************************
         SPACE
         EXEC  CICS RETURN                                             X
               COMMAREA(COMMAR)                                        X
               TRANSID(EIBTRNID)
         EJECT
DORECV   DS    0H
         SPACE
***********************************************************************
* PROCESS THE USERS INPUT                                             *
* -----------------------                                             *
*                                                                     *
*  This routine is called after the user has responded to the         *
*  signon screen.                                                     *
*                                                                     *
***********************************************************************
         SPACE
         L     R4,DFHEICAP             -> COMMAREA
         MVC   COMMAR,0(R4)            MOVE TO DYNAMIC STORAGE
         SPACE
***********************************************************************
* SET UP TO HANDLE A MAPFAIL CONDITION                                *
***********************************************************************
         SPACE
         EXEC  CICS HANDLE CONDITION                                   X
               MAPFAIL(LOGOFF)
         SPACE
         EXEC  CICS RECEIVE                                            X
               MAP('ISSIGN')                                           X
               MAPSET('ISSIGNM')
         SPACE
***********************************************************************
* PROCESS THE USERS INPUT                                             *
***********************************************************************
         SPACE
         CLI   EIBAID,DFHPF3           PF3 - LOGOFF ?
         BE    LOGOFF                  YES, DO SO
         SPACE
         CLI   EIBAID,DFHPF15          PF15 - LOGOFF ?
         BE    LOGOFF                  YES, GO TO VTAM
         SPACE
         CLC   SPUSERI,BLANKS          WAS A USERID ENTERED ?
         BE    NOUSER                  NO
         SPACE
         CLC   SPUSERI,NULLS           USERID FIELD ERASED ?
         BE    NOUSER                  YES, TREAT AS MISSING
         SPACE
         MVC   USERID,SPUSERI          SIGN ON WITH THIS ID
         SPACE
         CLC   SPPASSWI,BLANKS         WAS PASSWORD GIVEN ?
         BNE   CHECKNP                 YES, GO CHECK NEW PASSWORD
         SPACE
         LA    R5,ERRM1                -> ERROR MESSAGE 1
         MVC   SPPASSWL,PUTCURS        POSITION CURSOR
         B     PUTERR                  PUT OUT AN ERROR MESSAGE
         SPACE
CHECKNP  DS    0H
         CLC   SPNPASSI,BLANKS         A NEW PASSWORD REQUIRED ?
         BE    NONEWP                  NO
         CLC   SPNPASSI,SPVPASSI       NEW = VERIFY ?
         BE    DOSIGN                  YES, GO SIGNON
         SPACE
NPASSER  DS    0H
         LA    R5,ERRM2                -> ERROR MESSAGE
         MVC   SPNPASSL,PUTCURS        POSITION CURSOR
         MVC   SPNPASSO,BLANKS         BLANK OUT THE NEW PASSWORD
         MVC   SPVPASSO,BLANKS         BLANK OUT THE VER PASSWORD
         B     PUTERR                  GO THROW IT OUT
         SPACE
NONEWP   DS    0H
         CLC   SPVPASSI,BLANKS         VERIFY PASSWORD ALSO BLANK ?
         BNE   NPASSER                 NO, TELL THE USER
         SPACE
         B     DOSIGN                  GO DO RACF SIGNON
         SPACE
NOUSER   DS    0H
         LA    R5,ERRM4                -> ERROR MESSAGE 6
         MVC   SPUSERL,PUTCURS         POSITION CURSOR
         B     PUTERR                  PUT OUT AN ERROR MESSAGE
         SPACE
DOSIGN   DS    0H
         SPACE
***********************************************************************
* PERFORM A CICS SIGNON                                               *
***********************************************************************
         SPACE
         BAL   R9,SIGNON               GO PERFORM THE RACF SIGNON
         SPACE
***********************************************************************
* If the signon failed for any reason, we will blank out the new      *
* password fields. We want the changing of a password to be a         *
* deliberate act, and not a mistake.                                  *
***********************************************************************
         SPACE
         MVC   SPNPASSO,BLANKS         BLANK OUT THE NEW PASSWORD
         MVC   SPVPASSO,BLANKS         BLANK OUT THE VER PASSWORD
         SPACE
         LTR   R15,R15                 SIGNON COMPLETE ?
         BNZ   PUTERR                  NO, GO ISSUE AN ERROR MESSAGE
         SPACE
***********************************************************************
* THE SIGNON TO RACF IS COMPLETE.                                     *
* ------------------------------                                      *
*                                                                     *
*    * If the user specified an initial transaction then              *
*      initiate it.                                                   *
*                                                                     *
*    * Otherwise run the standard CICS good morning transaction       *
*                                                                     *
***********************************************************************
         SPACE
         MVC   TRANSID,GMTRAN          Assume standard Hello
         SPACE
         CLC   SPNXTI,BLANKS           Did the punter specify TRANID ?
         BE    GOTRAN                  No, go to standard
         MVC   TRANSID,SPNXTI          Yes, go to it
         SPACE
GOTRAN   DS    0H
         EXEC  CICS IGNORE CONDITION                                   x
               TRANSIDERR
         SPACE
         EXEC  CICS START                                              X
               TERMID(EIBTRMID)                                        X
               TRANSID(TRANSID)
         SPACE
         EXEC  CICS RETURN
         EJECT
LOGOFF   DS    0H
         SPACE
***********************************************************************
* This routine is branched to if a mapfail condition occurs while     *
* reading a map. This condition can be cause by the use of a short-   *
* read key (CLEAR,PA1 or PA2), or if there is no input (the user hit  *
* erase-input). In all cases, we will disconnect to VTAM.             *
* we also come here if the user hits PF3 or PF15.                     *
***********************************************************************
         SPACE
         EXEC  CICS ISSUE                                              X
               DISCONNECT
         SPACE
         EXEC  CICS RETURN
         EJECT
PUTERR   DS    0H
         SPACE
***********************************************************************
* PUT OUT AN ERROR MESSAGE                                            *
*                                                                     *
*    R5 -> ERROR MESSAGE TEXT                                         *
*                                                                     *
***********************************************************************
         SPACE
         MVC   SPERRMO,0(R5)
         SPACE
         EXEC  CICS SEND                                               X
               MAPSET('ISSIGNM')                                       X
               MAP('ISSIGN')                                           X
               DATAONLY                                                X
               CURSOR                                                  X
               FREEKB                                                  X
               ALARM
         SPACE
         B     RETURN
         EJECT
SIGNOFF  DS    0H
         SPACE
***********************************************************************
* This routine is called to check if a user is already signed on at   *
* the users terminal. If one is, it is signed off with a call to      *
* CICS security.                                                      *
*                                                                     *
* The manual states that ASSIGN UNATTEND returns an indication of     *
* if a user is signed on. This is total hog-poo. The test always      *
* returns X'00'. So we test here to see if the default id is logged   *
* on.                                                                 *
***********************************************************************
         SPACE
         EXEC  CICS ASSIGN                                             X
               USERID(SONID)
         SPACE
         EXEC  CICS INQUIRE SYSTEM                                     X
               DFLTUSER(DEFID)
         SPACE
         CLC   SONID,DEFID             Default ID logged on ?
         BE    SKIPSOF                 Yes, so skip signoff
         SPACE
         EXEC  CICS SIGNOFF
         SPACE
SKIPSOF  DS    0H
         BR    R9
         EJECT
SIGNON   DS    0H
         SPACE
***********************************************************************
* This routine is called to sign on to CICS using EXEC CICS SIGNON    *
***********************************************************************
         SPACE
         MVC   USER,USERID             MOVE USERID TO RACF AREA
         OC    USER,BLANKS             MAKE IT UPPER CASE
         SPACE
         MVC   PASSWD,SPPASSWI         MOVE PASSWORD TO RACF AREA
         OC    PASSWD,BLANKS           ENSURE UPPER CASE
         SPACE
         MVC   NPASSWD,SPNPASSI        MOVE NEW PASSWORD TO RACF AREA
         OC    NPASSWD,BLANKS          MAKE UPPER CASE
         SPACE
         EXEC CICS SIGNON                                              X
               USERID(USER)                                            X
               PASSWORD(PASSWD)                                        X
               NEWPASSWORD(NPASSWD)                                    X
               RESP(RESP)                                              X
               RESP2(RESP2)
         SPACE
         CLC   RESP,DFHRESP(NORMAL)   All ok ?
         BNE   SONERR                 No, process error return
***********************************************************************
* Signon completed successfully - clear R15 and return                *
***********************************************************************
         SPACE
         SR    R15,R15                 SIGNED ON OK ?
         BR    R9                      EXIT TO CALLER
         SPACE
***********************************************************************
* Signon failed - analyse reason and tell the user                    *
***********************************************************************
SONERR   DS    0H
         L     R15,RESP2               Get extended response code
         SPACE
***********************************************************************
* Look up error message matching response code                        *
***********************************************************************
         SPACE
         LA    R5,ERRTAB               -> Error Table
ERRLKUP  DS    0H
         CLI   0(R5),X'FF'             End of table ?
         BE    ERRRET                  Yes, return with last message
         SPACE
         C     R15,0(R5)               Match on this one ?
         BE    ERRRET                  Yes
         SPACE
         LA    R5,12(R5)               -> Next message
         B     ERRLKUP                 Keep on looking
         SPACE
ERRRET   DS    0H
         L     R4,8(R5)                -> Cursor position code
         L     R5,4(R5)                -> Message text
         BR    R4                      Go posn cursor and exit
         SPACE
EXPIRED  DS    0H
         SPACE
CURSPW   DS    0h
         MVC   SPPASSWL,PUTCURS        PUT CURSOR IN PASSWORD
         BR    R9                      RETURN TO CALLER
         SPACE
CURSNPW  DS    0h
         MVC   SPNPASSL,PUTCURS        PUT CURSOR IN NEW PASSWORD
         BR    R9                      RETURN TO CALLER
         SPACE
CURSUS   DS    0h
         MVC   SPUSERL,PUTCURS         PUT CURSOR IN USERID
         BR    R9                      RETURN TO CALLER
         EJECT
PUTCURS  DC    H'-1'
MAXGMM   DC    H'80'
H4       DC    H'4'
H5       DC    H'5'
H8       DC    H'8'
H16      DC    H'16'
         SPACE
GMTRAN   DC    C'CSGM'           Standard CICS Hello Message
LOGO     DC    C'LOGO'           LOGOFF transaction ID
         SPACE
BLANKS   DC    CL9' '
NULLS    DC    XL9'000000000000000000'
         SPACE
**********************************************************************
* Error message table                                                *
*====================================================================*
*  Cursor posn routine addr ------+                                  *
*                                 |                                  *
*  Error Msg Addr ------+         |                                  *
*                       |         |                                  *
*  RESP2 code ---+      |         |                                  *
*                |      |         |                                  *
*                v      v         v                                  *
**********************************************************************
         SPACE
ERRTAB   DS    0F
         DC    A(01),A(ERRM5),A(CURSPW)
         DC    A(02),A(ERRM6),A(CURSPW)
         DC    A(03),A(ERRM3),A(CURSPW)
         DC    A(04),A(ERRM7),A(CURSNPW)
         DC    A(08),A(ERRM13),A(CURSUS)
         DC    A(16),A(ERRM8),A(CURSUS)
         DC    A(17),A(ERRM9),A(CURSPW)
         DC    A(19),A(ERRM10),A(CURSUS)
         DC    A(20),A(ERRM11),A(CURSUS)
         DC    A(29),A(ERRM12),A(CURSUS)
         DC    X'FFFFFFFF'
         DC    A(ERRM14),A(CURSUS)
         SPACE
ERRM1    DC    CL70'Please enter your password'
ERRM2    DC    CL70'New password does not equal the verify password, plX
               ease reenter'
ERRM3    DC    CL70'Your password has expired - Please enter new passwoX
               rd (TWICE)'
ERRM4    DC    CL70'Please enter Your Userid and Password'
ERRM5    DC    CL70'Please enter your password'
ERRM6    DC    CL70'The password you entered is incorrect'
ERRM7    DC    CL70'The new password is not acceptable'
ERRM8    DC    CL70'You are not authorised to use this terminal'
ERRM9    DC    CL70'You are not authorised to use this system'
ERRM10   DC    CL70'Your userid is currently revoked'
ERRM11   DC    CL70'Your userid is revoked from this group'
ERRM12   DC    CL70'Your userid is already signed on'
ERRM13   DC    CL70'Userid entered is not defined to security system'
ERRM14   DC    CL70'Undetermined security error occured'
         LTORG
         PRINT ON,NOGEN
         COPY  DFHCSADS
         DFHSIT TYPE=DSECT
         COPY  DFHTCADS
         DFHTCA CICSYST=YES
         COPY  DFHTCTTE
         END
MEMBER NAME  ISSIGNM
ISSIGNM  DFHMSD TYPE=&SYSPARM,MODE=INOUT,STORAGE=AUTO
         TITLE 'CICS SIGNON PANEL               '
ISSIGN   DFHMDI SIZE=(24,80),CTRL=(FREEKB),MAPATTS=(COLOR),            *
               COLUMN=1,LINE=1,DATA=FIELD,TIOAPFX=YES,                 *
               OBFMT=NO
* SPDATE
SPDATE   DFHMDF POS=(1,1),LENGTH=8,INITIAL='        ',                 *
               ATTRB=(PROT,BRT),COLOR=TURQUOISE
         DFHMDF POS=(1,10),LENGTH=1,ATTRB=(PROT,NORM)
         DFHMDF POS=(1,22),LENGTH=39,INITIAL=' ** Your Company Name ** *
               CICS Signon ',ATTRB=(PROT,BRT)
* SPTIME
SPTIME   DFHMDF POS=(1,72),LENGTH=8,INITIAL='        ',                *
               ATTRB=(PROT,BRT),COLOR=TURQUOISE
         DFHMDF POS=(2,1),LENGTH=1,ATTRB=(PROT,NORM)
* SPGMMT
SPGMMT   DFHMDF POS=(2,80),LENGTH=80,INITIAL='                         *
                                                                      '*
               ,ATTRB=(PROT,BRT),COLOR=YELLOW
         DFHMDF POS=(4,1),LENGTH=1,ATTRB=(PROT,NORM)
         DFHMDF POS=(4,80),LENGTH=39,INITIAL='Enter your userid and pas*
               sword below :-',ATTRB=(PROT,NORM)
         DFHMDF POS=(5,80),LENGTH=15,INITIAL='Otherwise press',        *
               ATTRB=(PROT,NORM)
         DFHMDF POS=(6,16),LENGTH=3,INITIAL='PF3',ATTRB=(PROT,         *
               BRT)
         DFHMDF POS=(6,20),LENGTH=19,INITIAL='to logoff from CICS',    *
               ATTRB=(PROT,NORM)
         DFHMDF POS=(8,2),LENGTH=7,INITIAL=' Userid',                  *
               ATTRB=(PROT,BRT),COLOR=TURQUOISE
         DFHMDF POS=(8,10),LENGTH=1,ATTRB=(PROT,NORM)
         DFHMDF POS=(8,16),LENGTH=1,INITIAL=':',ATTRB=(PROT,           *
               NORM)
* SPUSER
SPUSER   DFHMDF POS=(8,18),LENGTH=8,INITIAL='        ',                *
               ATTRB=(UNPROT,NORM,IC,FSET)
         DFHMDF POS=(8,27),LENGTH=1,ATTRB=(ASKIP,NORM)
         DFHMDF POS=(10,2),LENGTH=9,INITIAL=' Password',               *
               ATTRB=(PROT,BRT),COLOR=TURQUOISE
         DFHMDF POS=(10,12),LENGTH=5,INITIAL='    :',                  *
               ATTRB=(PROT,NORM)
* SPPASSW
SPPASSW  DFHMDF POS=(10,18),LENGTH=8,INITIAL='        ',               *
               ATTRB=(UNPROT,DRK,FSET)
         DFHMDF POS=(10,27),LENGTH=1,ATTRB=(PROT,NORM)
         DFHMDF POS=(12,80),LENGTH=62,INITIAL='To change your password *
               enter your new password below twice :-',                *
               ATTRB=(PROT,NORM)
         DFHMDF POS=(15,2),LENGTH=13,INITIAL=' New Password',          *
               ATTRB=(PROT,BRT),COLOR=TURQUOISE
         DFHMDF POS=(15,16),LENGTH=1,INITIAL=':',ATTRB=(PROT,          *
               NORM)
* SPNPASS
SPNPASS  DFHMDF POS=(15,18),LENGTH=8,INITIAL='        ',               *
               ATTRB=(UNPROT,DRK,FSET)
         DFHMDF POS=(15,27),LENGTH=1,ATTRB=(ASKIP,NORM)
         DFHMDF POS=(17,16),LENGTH=1,INITIAL=':',ATTRB=(PROT,          *
               NORM)
* SPVPASS
SPVPASS  DFHMDF POS=(17,18),LENGTH=8,INITIAL='        ',               *
               ATTRB=(UNPROT,DRK,FSET)
         DFHMDF POS=(17,27),LENGTH=1,ATTRB=(ASKIP,NORM)
* SPERRM
SPERRM   DFHMDF POS=(22,5),LENGTH=70,INITIAL='                         *
                                                            ',         *
               ATTRB=(PROT,BRT),COLOR=RED
         DFHMDF POS=(22,76),LENGTH=7,INITIAL='    Nxt',                *
               ATTRB=(PROT,NORM)
* SPNXT
SPNXT    DFHMDF POS=(23,4),LENGTH=4,INITIAL='    ',                    *
               ATTRB=(UNPROT,NORM,IC,FSET)
* SPTERM
SPTERM   DFHMDF POS=(24,1),LENGTH=4,INITIAL='    ',ATTRB=(ASKIP,       *
               BRT),COLOR=TURQUOISE
         DFHMDF POS=(24,6),LENGTH=1,ATTRB=(PROT,NORM)
         DFHMDF POS=(24,19),LENGTH=9,INITIAL='Clear/PF3',              *
               ATTRB=(PROT,BRT)
         DFHMDF POS=(24,29),LENGTH=30,INITIAL='- Disconnect from CICS s*
               ervice',ATTRB=(PROT,NORM)
* SPSYS
SPSYS    DFHMDF POS=(24,72),LENGTH=8,INITIAL='        ',               *
               ATTRB=(PROT,BRT),COLOR=TURQUOISE
         DFHMSD TYPE=FINAL
         END
