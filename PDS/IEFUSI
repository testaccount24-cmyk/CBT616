IEFUSI   CSECT
IEFUSI   AMODE 31
IEFUSI   RMODE ANY
*---------------------------------------------------------------------*
* IEFUSI                                                              *
* ------                                                              *
*                                                                     *
*   This exit is used to control the region size above and below the  *
*   16M line.                                                         *
*                                                                     *
*   IEFUSI is called at job-step initiation prior to allocation       *
*                                                                     *
*   Input:  R1  - Address of parameter list                           *
*           R13 - Register save area                                  *
*           R14 - Return address                                      *
*           R15 - Entry point of IEFUSI                               *
*                                                                     *
*   Output: R15 - Return code (0 or 4)                                *
*                 Sizes set in VSM parameter list                     *
*                                                                     *
*   Function: As delivered, OS/390 limits region sizes above and      *
*             below the line based on the JCL REGION parameter,       *
*             and the default IEALIMIT exit.                          *
*                                                                     *
*             This exit will in certain cases, overide the REGION     *
*             size requested both above and below the line            *
*                                                                     *
*             In particular it allows CICS regions to use more than   *
*             32M above the line.                                     *
*                                                                     *
*             Detailed Function:                                      *
*                                                                     *
*               * If the step is V=R or requested a region of 0K,     *
*                 no action is taken by this exit                     *
*                                                                     *
*               * If the requested region is less than a minimum      *
*                 amount it is increased to the minimum.              *
*                                                                     *
*               * If the requested below-line region is too large     *
*                 it is reduced to the maximum.                       *
*                                                                     *
*               * The below line limit and region are adjusted so     *
*                 that a variable getmain cannot grab the entire      *
*                 region.                                             *
*                                                                     *
*               * If the requested region does not explictly specify  *
*                 the size of the above line region (IE it is < 16M)  *
*                 and the program being run is CICS, the above line   *
*                 region is increased above the default of 32M        *
*                                                                     *
*               * If an above the line region is requested, it is     *
*                 limited to a maximum.                               *
*                                                                     *
*                                                                     *
* Author: Nigel Thomas - March 1998                                   *
*---------------------------------------------------------------------*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
         USING IEFUSI,R15
         B     AROUND
         DC    CL8'IEFUSI'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
AROUND   DS    0H
         SAVE  (14,12)
         LR    R12,R15                 Get Entry into base reg
         DROP  R15
         USING IEFUSI,R12
         LM    R5,R8,8(R1)             Load input addresses
         USING VSMPARML,R7
         SPACE
         USING FLAGWORD,R8
         SPACE
         TM    VRFLAG,X'80'            Is this step V=R ?
         BO    GOBACK                  Yes, then leave well alone
         SPACE
*--------------------------------------------------------------------*
* Get the maximum below the line region size from the LDA            *
*--------------------------------------------------------------------*
         SPACE
         L     R3,16                   -> CVT
         L     R3,0(R3)                -> TCB/ASCB words
         L     R3,12(R3)               -> current ASCB
         USING ASCB,R3
         L     R3,ASCBLDA              -> LDA
         DROP  R3
         USING LDA,R3
         L     R2,LDASIZA              Get Maximum < 16M Region
         DROP  R3
         SPACE
         S     R2,F512K                Reduce max limit by 512K
         LR    R3,R2                   Get maximum region limit
         S     R3,F256K                Reduce max size by 256K
         SPACE
         L     R4,VSMREQST             Get requested region size
         LTR   R4,R4                   Zero - IE No limit ?
         BZ    GOBACK                  Yes, his funeral
         SPACE
         OI    VSMFLAGS,B'10100000'    Tell VSM to use IEFUSI values
*                                      and to check availability
*                                      above and below the line
         SPACE
*--------------------------------------------------------------------*
* Set the size and limit below the 16m line                          *
*--------------------------------------------------------------------*
         SPACE
         C     R4,MINBELOW             Is requested region too small ?
         BNL   CHECK16                 No, leave it be
         L     R4,MINBELOW             Yes, increase to minimum
         SPACE
CHECK16  DS    0H
         CR    R4,R3                   Is requested region > Max ?
         BH    LIMIT16                 Yes, so limit it
         SPACE
         ST    R4,VSMSZEBL             No, Set size below
         A     R4,F256K                Bump by 256K
         ST    R4,VSMLIMBL             Set below limit
         B     CHECK31                 Go check above limit
         SPACE
LIMIT16  DS    0H
         ST    R3,VSMSZEBL             Set the size below
         ST    R2,VSMLIMBL             Set the limit below
         SPACE
CHECK31  DS    0H
*--------------------------------------------------------------------*
* Set the size and limit above the 16m line                          *
*--------------------------------------------------------------------*
         SPACE
         L     R2,VSMREQST             Get requested region size
         LR    R3,R2                   set as limit also
         C     R2,F16M                 Region requested > 16M ?
         BH    CHECKVAL                Yes, check specified value
         SPACE
         L     R2,F32M                 Set default limit to 32M
         LR    R3,R2                   Set default size to 32M
         SPACE
         CLC   0(8,R5),CICS            Is this CICS ?
         BNE   SET31                   No, use defaults as is
         SPACE
         L     R2,CICSLIM              Use CICS limit
         L     R3,CICSSIZ              Use CICS size
         B     SET31                   go set region values
         SPACE
CHECKVAL DS    0H
         C     R3,MAXABOVE             Too much above the line ?
         BNH   CHECKLOW                no, Allow region
         L     R2,MAXABOVE             Limit to maximum
         LR    R3,R2                   ditto size
         B     SET31                   go set
CHECKLOW DS    0H
         C     R3,F32M                 Too low ?
         BH    SET31                   no
         L     R2,F32M                 Use default of 32M
         LR    R3,R2                   Ya Ya
         SPACE
SET31    DS    0H
         ST    R3,VSMSZEAB             Set above line size
         ST    R2,VSMLIMAB             Set above line limit
GOBACK   DS    0H
         LM    R14,R12,12(R13)         Restore regs
         SR    R15,R15                 Zero return code
         BR    R14                     Exit to caller
         SPACE
         EJECT
CICS     DC    CL8'DFHSIP'             CICS program name
         DS    0F
F256K    DC    AL4(256*1024)           256K
F512K    DC    AL4(512*1024)           512K
F16M     DC    AL4(16*1024*1024)       16M
F32M     DC    AL4(32*1024*1024)       32M
         SPACE
CICSLIM  DC    AL4(64*1024*1024)       64M
CICSSIZ  DC    AL4(58*1024*1024)       58M
         SPACE
MINBELOW DC    AL4(1*1024*1024)        Min size below (1M)
MAXABOVE DC    AL4(64*1024*1024)       Max size above (64M)
         LTORG
FLAGWORD DSECT
VRFLAG   DS    F                       V=R Flag
VSMPARML DSECT
VSMFLAGS DS    F                       Flag Word
*              0... ....               IEALIMIT is suplying values
*              1... ....               IEFUSI is suplying values
*              .0.. ....               Check to see request fits below
*              .1.. ....               Skip below size check
*              ..0. ....               Skip above size check
*              ..1. ....               Check above size fits
VSMREQST DS    F                       Region size requested on JCL
VSMLIMBL DS    F                       Region limit below 16M
VSMSZEBL DS    F                       Region size below 16m
VSMLIMAB DS    F                       Region limit above 16M
VSMSZEAB DS    F                       Region size above 16m
         IHAASCB
         IHALDA
         END
